---Compliance Report Page Measures---


---Percent Change: The percent change in compliant metrics from the previous month---

% CHANGE = 

VAR PREVMONTH = 

    CALCULATE(
      SUM(MonthlySummaries[Compliant]), 
        MonthlySummaries[Business_Area] <> "Overall", ---This filter is only necessary if you have specific values you want to omit from the formula---
          MONTH('MonthlySummaries'[Date])=MONTH(NOW())-2, ---This data is a month behind the actual date, thus the -2 here.---
            YEAR('MonthlySummaries'[Date])=YEAR(NOW()))        ---If you wanted to use the actual previous month, use -Month(Now())-1---

VAR CURRENTMONTH = 

    CALCULATE(
      SUM(MonthlySummaries[Compliant]), 
        MonthlySummaries[Business_Area] <> "Overall", ---See comment in the previous month variable---
           MONTH('MonthlySummaries'[Date])=MONTH(NOW())-1, ---If you wanted to use the actual current month, use MONTH(NOW())---
             YEAR('MonthlySummaries'[Date])=YEAR( NOW()))

VAR DIFF = CURRENTMONTH-PREVMONTH

VAR PERCENTCHANGE = DIVIDE(DIFF,PREVMONTH)

RETURN 

  PERCENTCHANGE


---Compliance Percent: The percent of all metrics that are compliant---

COMPLIANCE % = CALCULATE(DIVIDE(SUM(MonthlySummaries[Compliant]),SUM(MonthlySummaries[Total_Metrics])))


---Compliance Rate Title: A measure to show the current month in the title of the Compliance Rate card---

COMPLIANCE RATE TITLE = 

      "Compliance Rate " & "(" 
          & FORMAT(EOMONTH(TODAY(),-1), ---If you wanted to use the actual current month, use EOMONTH(TODAY())
                "mmmm YYYY" &")"
)


---Current Year Compliant Rate: The percent of all metrics that are compliant for the current year---

CY COMPLIANT RATE = 

VAR CYSUM =

    CALCULATE(SUM(MonthlySummaries[Total_Metrics]),
       FILTER('MonthlySummaries',YEAR('MonthlySummaries'[Date])=YEAR(NOW())))

VAR CYCOMPSUM =

     CALCULATE(SUM('MonthlySummaries'[Compliant]),
       FILTER('MonthlySummaries',YEAR('MonthlySummaries'[Date].[Date])=YEAR(NOW())))

VAR CYCOMPRATE = 

    CYCOMPSUM/CYSUM

RETURN

  CYCOMPRATE


---Last (Previous) Year Compliant Rate: The percent of all metrics that are compliant for the previous year---

LY COMPLIANT RATE = 

VAR LYSUM =

    CALCULATE(SUM('MonthlySummaries'[Total_Metrics] ),
       FILTER('MonthlySummaries', YEAR('MonthlySummaries'[Date])=YEAR(NOW())-1))


VAR LYCOMPSUM =

    CALCULATE(SUM('MonthlySummaries'[Compliant]),
       FILTER('MonthlySummaries',YEAR('MonthlySummaries'[Date])=YEAR(Now())-1))

VAR LYCOMPRATE = 

    LYCOMPSUM / LYSUM

RETURN

   LYCOMPRATE


---Max Month Compliant Rate: The percent of all metrics that are compliant for the most current (max) month---

[MAX MONTH COMPLIANT] = 

VAR MAXMONTH = 

    CALCULATE(MIN(MonthlySummaries[Compliance_Percent]),
      FILTER(MonthlySummaries, MonthlySummaries[Date]=MAX(MonthlySummaries[Date])))

RETURN 

  IF(ISBLANK(MAXMONTH),0,MAXMONTH)


---Year to Date Compliant Rate: The year to date compliant rate for the current year---

YTD COMPLIANT = 

VAR YTDCOMP = 

    TotalYTD(AVERAGE(MonthlySummaries[Compliance_Percent]),
      MonthlySummaries[Date])

RETURN 

  IF(ISBLANK(YTDCOMP),0,YTDCOMP)





